package binarioadecimal.cup;
import java_cup.runtime.Symbol;
 parser code 
{:

    public void syntax_error(Symbol s){
        String lexema = s.value.toString();
        int fila = s.right;
        int columna = s.left;
        System.out.println("Error Sintactico Recuperado");
        System.out.println("\t \tLexema: "+ lexema);
        System.out.println("\t \tFila: "+ fila);
        System.out.println("\t \tColumna: "+ columna);

    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{
        String lexema = s.value.toString();
        int fila = s.right;
        int columna = s.left;
        System.out.println("Error Sintactico Panic Mode");
        System.out.println("\t \tLexema: "+ lexema);
        System.out.println("\t \tFila: "+ fila);
        System.out.println("\t \tColumna: "+ columna);

    }

:}



action code
{:
private static final int CONVERTIDOR=2;
:}




/* Terminals*/
terminal PUNTO;
terminal String    BIN, ERROR;  

/* Non-terminals */
non terminal            Inicio,Binario,E,D,Bin;

start with Inicio;
Inicio::= Binario:bi {:System.out.println("El Numero Decimal Es "+bi);:};

Binario::= E:e PUNTO D:d {:RESULT=(Float)e+(Float)d;:}
        |E:e    {:RESULT=(Float)e;:};
E::= E:e Bin:b      {:RESULT=(Float)e*CONVERTIDOR+(Float)b;:}
    |Bin:b          {:RESULT=(Float)(b);:};

D::= Bin:b D:d      {:RESULT=(Float)d/CONVERTIDOR+(Float)b/CONVERTIDOR;:}
    |Bin:b          {:RESULT=(Float)(b)/CONVERTIDOR;:};

Bin::= BIN:b{:RESULT=Float.parseFloat(b);:};
